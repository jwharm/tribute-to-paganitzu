plugins {
    id 'application'
    id 'idea'
}

group = 'io.github.jwharm.tribute'
version = '0.1'

repositories {
    mavenCentral()
}

idea {
    module {
        downloadSources = true
    }
}

dependencies {
    implementation 'io.github.jwharm.javagi:adw:0.10.1'
    implementation 'io.github.jwharm.cairobindings:cairo:1.18.3'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

tasks.register('glibCompileResources') {
    doLast {
        exec {
            workingDir 'src/main/resources'
            commandLine 'glib-compile-resources', 'game.gresource.xml'
        }
    }
}

tasks.named('classes') {
    dependsOn glibCompileResources
}

tasks.named('run', JavaExec) {
    jvmArgs += '--enable-native-access=ALL-UNNAMED'
}

application {
    mainClass = 'io.github.jwharm.tribute.Main'
}
